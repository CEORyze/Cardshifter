sourceCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

description = 'cardshifter-api'

jar {
    from('src/main/java') {
        include '**/*.java'
        include '**/*.gwt.xml'
    }
}

apply plugin: 'maven'

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

def getMavenSettingsCredentials = {
    String userHome = System.getProperty('user.home')
    File mavenSettings = new File(userHome, '.m2/settings.xml')
    def xmlSlurper = new XmlSlurper()
    def output = xmlSlurper.parse(mavenSettings)
    return output."servers"."server"
}

def getCredentials = {
    def entries = getMavenSettingsCredentials()
    for (entry in entries) {
        if (entry."id".text() == 'cardshifter-deploy') {
            return [username: entry.username.text(), password: entry.password.text()]
        }
    }
}

uploadArchives {
    def creds = getCredentials()
    if (!creds) {
        return;
    }
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "ftp://www.zomis.net/public_html/maven") {
                authentication(userName: creds["username"], password: creds["password"])
            }
        }
    }
}
